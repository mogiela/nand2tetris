// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * Graphic screen library.
 */
class Screen {

    static boolean isBlack;
    static int mod16;
    static Array twoToThe;
    static int screen_base;
    /** Initializes the Screen. */
    function void init() {
        let isBlack = true;
        let mod16 = 15;
        let screen_base = 16384;

        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10]= 1024;
        let twoToThe[11]= 2048;
        let twoToThe[12]= 4096;
        let twoToThe[13]= 8192;
        let twoToThe[14]= 16384;
        let twoToThe[15]=~32767;

        return;
    }

    /** Erases the whole screen. */
    function void clearScreen() {
        var int i;

        let i = 0;
        while (i < 8192) {
            do Memory.poke(i+screen_base, 0);
            let i = i + 1;
        }
        return;
    }

    /** Sets the color to be used in further draw commands
     *  where white = false, black = true. */
    function void setColor(boolean b) {
        let isBlack = b;
        return;
    }

    /** Draws the (x, y) pixel. */
    function void drawPixel(int x, int y) {
        var int raddr;
        var int baddr;
        // fix this with fast division
        let raddr = screen_base + (Math.fastBinaryMulter(y, 5)) + (x/16);
        do Memory.poke(raddr, Screen.setBitOfNum(Memory.peek(raddr), x & mod16, isBlack));
        return;
    }

    function int setBitOfNum(int num, int bitPos, boolean bitVal) {
        // bitPos should be smaller than 16 but just in case we take its mod16
        if (bitVal){
            return num | twoToThe[bitPos&mod16];
        }
        else{
            return num & (~twoToThe[bitPos&mod16]);
        }
    }

    /** Draws a line from (x1, y1) to (x2, y2). */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int dx, dy, curX, curY, diff, raddr, curBlock, xmod16, yInfo;
        var boolean dirLeft, dirUp;
        let dx = x2-x1;
        let dy = y2-y1;
        let raddr = screen_base + (Math.fastBinaryMulter(y1, 5)) + (x1/16);

        if (dy < 0){ // going down-top
            let dy = -dy;
            let dirUp = true;
        }
        if (dx < 0){ // going right-left
            let dx = -dx;
            let dirLeft = true;
        }

        if (dy = 0){  // straight horizontal line
            let xmod16 = x1&mod16;
            if (dirLeft){
                if (~(xmod16=mod16)){ // not in a block start
                    let curBlock = Memory.peek(raddr);
                    if((dx+1)<xmod16){ // can't color the rest of the block
                        if(isBlack){
                            let curBlock = curBlock | ((twoToThe[xmod16]-1)-(twoToThe[(x2&mod16)-1]-1)); // black
                        }
                        else{
                            let curBlock = curBlock & (~((twoToThe[xmod16]-1)-(twoToThe[(x2&mod16)-1]-1))); // white
                        }
                        do Memory.poke(raddr, curBlock); // write and return
                        return;


                        // while(xmod16<(dx+1)) { // advance until line end
                        //     if(isBlack){
                        //         let curBlock = curBlock | (twoToThe[16-xmod16]); // black
                        //     }
                        //     else{
                        //         let curBlock = curBlock & (~twoToThe[16-xmod16]); // white
                        //     }
                        //     let xmod16 = xmod16 + 1;
                        // }
                        // do Memory.poke(raddr, curBlock); // write and return
                        // return;
                    }
                    else{// we can color the rest of the block
                        if(isBlack){
                            let curBlock = curBlock | (twoToThe[xmod16]-1); // black
                        }
                        else{
                            let curBlock = curBlock & (~(twoToThe[xmod16]-1)); // white
                        }
                        do Memory.poke(raddr, curBlock);
                        let x1 = x1-xmod16-1;
                        let raddr = raddr - 1;
                    }
                }

                if((dx+1)<xmod16){ // we're in a block start but we cant color all the block
                    if(isBlack){
                        let curBlock = curBlock | (twoToThe[x2&mod16]-1); // black
                    }
                    else{
                        let curBlock = curBlock & (~(twoToThe[x2&mod16]-1)); // white
                    }
                    do Memory.poke(raddr, curBlock); // write and return
                    return;
                }

                while((x1-15)>x2){ // if we can fill a full block, do it
                    do Memory.poke(raddr, isBlack);
                    let raddr = raddr - 1;
                    let x1 = x1 - 16;
                }
                let curBlock = Memory.peek(raddr); // there is less than a block to go
                if(isBlack){
                    let curBlock = curBlock | (-twoToThe[(x2&mod16)]); // black
                }
                else{
                    let curBlock = curBlock & (~(-twoToThe[(x2&mod16)])); // white
                }
                do Memory.poke(raddr, curBlock);
                return;     // doneeee...
            }
            else{  // going right

                if (~(xmod16=0)){ // is the same as ~(xmod16=0) and means we're not in a block start
                    let curBlock = Memory.peek(raddr);

                    if((16-xmod16)>dx){ // can't color the rest of the block (do the math, it's correct)
                        if(isBlack){
                            let curBlock = curBlock | ((twoToThe[x2&mod16]-1)-(twoToThe[xmod16-1]-1)); // black
                        }
                        else{
                            let curBlock = curBlock & (~((twoToThe[x2&mod16]-1)-(twoToThe[xmod16-1]-1))); // white
                        }
                        do Memory.poke(raddr, curBlock); // write and return
                        return;
                    }
                    else{// we can color the rest of the block
                        if(isBlack){
                            let curBlock = curBlock | (-twoToThe[xmod16-1]); // black
                        }
                        else{
                            let curBlock = curBlock & (~(-twoToThe[xmod16-1])); // white
                        }
                        do Memory.poke(raddr, curBlock);
                        let x1 = x1+16-xmod16;
                        let raddr = raddr + 1;
                    }
                }
                if((16-xmod16)>dx){ // we're in a block start but we cant color all the block
                    if(isBlack){
                        let curBlock = curBlock | (twoToThe[x2&mod16]-1); // black
                    }
                    else{
                        let curBlock = curBlock & (~(twoToThe[x2&mod16]-1)); // white
                    }
                    do Memory.poke(raddr, curBlock); // write and return
                    return;
                }
                while((x1+16)<x2){ // if we can fill a full block, do it
                    do Memory.poke(raddr, isBlack);
                    let raddr = raddr + 1;
                    let x1 = x1 + 16;
                }
                let curBlock = Memory.peek(raddr); // there is less than a block to go
                if(isBlack){
                    let curBlock = curBlock | (twoToThe[(x2&mod16)]-1); // black
                }
                else{
                    let curBlock = curBlock & (~(twoToThe[(x2&mod16)]-1)); // white
                }
                do Memory.poke(raddr, curBlock);
                return;     // doneeee...

            }
        }


        let curBlock = Memory.peek(raddr);
        while ((curX < dx) | (curY < dy)){ // imagining we're going right-down but fixing it later by getting the correct block
            // draw the pixel to the block:
            if (dirLeft){
                //draw pixel x-curX , y-curY
                let curBlock = Screen.setBitOfNum(curBlock, (x1-curX), isBlack);
            }
            else{
                //draw pixel x+curX , y-curY
                let curBlock = Screen.setBitOfNum(curBlock, (x1+curX), isBlack);
            }

            // update diff and curBlock
            if(~(diff > 0)){
                let curY = curY + 1;
                let diff = diff + dx;
                //write number to memory, decrease/increase raddr by 32, get new number.
                do Memory.poke(raddr, curBlock);
                if (dirUp){
                    let raddr = raddr - 32;
                }
                else{
                    let raddr = raddr + 32;
                }

                let curBlock = Memory.peek(raddr);
            }
            else{
                let curX = curX + 1;
                let diff = diff - dy;
                if (dirLeft){
                    // check if x%16 is 15
                    if((x1-curX)&mod16 = mod16){
                        // write number to memory, decrease raddr by 1, get new number
                        do Memory.poke(raddr, curBlock);
                        let raddr = raddr - 1;
                        let curBlock = Memory.peek(raddr);
                    }
                }else{
                    // check if x%16 is 0:
                    if((x1+curX)&mod16 = 0){
                        // write number to memory, increase raddr by 1, get new number
                        do Memory.poke(raddr, curBlock);
                        let raddr = raddr + 1;
                        let curBlock = Memory.peek(raddr);
                    }
                }
            }
        }
        do Memory.poke(raddr, curBlock);
        return;
    }

    /** Draws a filled rectangle where the top left corner
     *  is (x1, y1) and the bottom right corner is (x2, y2). */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int x1mod16, painter, x2mod16, curBlock;



        let x1mod16 = x1&mod16;
        let x2mod16 = x2&mod16;

        let x1 = screen_base + (Math.fastBinaryMulter(y1, 5)) // x1 is the pivot
            + (x1/16);
        let y2 = x1 + (Math.fastBinaryMulter(y2-y1, 5)) + 1; // y2 is the y-axis boundary, (the plus 1 is to evaluate <= instead of <)
        let x2 = screen_base + (Math.fastBinaryMulter(y1, 5)) // x2 is the x-axis boundary
                             + (x2/16);
        let y1 = x1;                                    // y1 is the iterator


        if (~(x1mod16=0)){ //  means we're not in a block start
            if(x1=x2){ // x1 and x2 are in the same block so we cant fill the whole block
                if(isBlack){
                    let painter = ((twoToThe[x2mod16]-1)-(twoToThe[x1mod16-1]-1));
                    while (y1 < y2){
                        let curBlock = Memory.peek(y1);
                        let curBlock = curBlock | painter; // black
                        do Memory.poke(y1, curBlock);
                        let y1 = y1 + 32;
                    }
                    return;
                }
                else{
                    let painter = (~(twoToThe[x2mod16]-1)-(twoToThe[x1mod16-1]-1));
                    while (y1 < y2){
                        let curBlock = Memory.peek(y1);
                        let curBlock = curBlock & painter; // black
                        do Memory.poke(y1, curBlock);
                        let y1 = y1 + 32;
                    }
                    return;
                }
            }
            else{               // color the whole block
                if(isBlack){
                    let painter = (-twoToThe[x1mod16-1]);
                    while (y1 < y2){
                        let curBlock = Memory.peek(y1);
                        let curBlock = curBlock | painter; // black
                        do Memory.poke(y1, curBlock);
                        let y1 = y1 + 32;
                    }
                }
                else{
                    let painter = ~(-twoToThe[x1mod16-1]);
                    while (y1 < y2){
                        let curBlock = Memory.peek(y1);
                        let curBlock = curBlock & painter; // black
                        do Memory.poke(y1, curBlock);
                        let y1 = y1 + 32;
                    }
                }
            }
        }



        // we try to color the whole block
        if(isBlack){
            let painter = -1;
        }
        else{
            let painter = 0;
        }
        let x1 = x1 + 1;
        let y1 = x1;
        let y2 = y2 + 1;
        while(x1<x2){           // going through all columns
            if(isBlack){
                while(y1<y2){           // a single column fill
                    let curBlock = Memory.peek(y1);
                    let curBlock = curBlock | painter; // black
                    do Memory.poke(y1, curBlock);
                    let y1 = y1 + 32;
                }
            }
            else{
                while(y1<y2){
                    let curBlock = Memory.peek(y1);
                    let curBlock = curBlock & painter; // white
                    do Memory.poke(y1, curBlock);
                    let y1 = y1 + 32;
                }
            }
            let x1 = x1 + 1;
            let y1 = x1;
            let y2 = y2 + 1;
        }

        // fill th last block
        if(isBlack){
            let painter = (twoToThe[x2mod16]-1);
            while(y1<y2){           // a single column full
                let curBlock = Memory.peek(y1);
                let curBlock = curBlock | painter; // black
                do Memory.poke(y1, curBlock);
                let y1 = y1 + 32;
            }
        }
        else{
            let painter = (~(twoToThe[x2mod16]-1));
            while(y1<y2){           // a single column full
                let curBlock = Memory.peek(y1);
                let curBlock = curBlock & painter; // white
                do Memory.poke(y1, curBlock);
                let y1 = y1 + 32;
            }
        }


        // while (~(y1 > y2)){
        //     do Screen.drawLine(x1, y1, x2, y1);
        //     let y1 = y1 + 1;
        // }
        return;
    }

    /** Draws a filled circle of radius r around (cx, cy). */
    function void drawCircle(int cx, int cy, int r) {
        /** calculated according to the midpoint circle algorithm
         *  with a few modifications to look a bit more like the school solution.
         */
        var int x, y, err;
        let x = r;

        do Screen.drawLine(cx-x, cy, cx+x, cy);
        while(~(x < y)){

            do Screen.drawLine(cx-y,cy-x , cx+y,cy-x);
            do Screen.drawLine(cx-x,cy-y , cx+x,cy-y);
            do Screen.drawLine(cx-x,cy+y , cx+x,cy+y);
            do Screen.drawLine(cx-y,cy+x , cx+y,cy+x);

            if (~(err > 0)){ // y += 1; err += 2y + 1
                let y = y + 1;
                let err = err + y + y + 1;
            }
            else{ // x -= 1; err -= 2x + 1
                let x = x - 1;
                let err = err - x - x - 1;
            }
        }
        return;
    }

    /* return a number with 0's in all bits except the pos bit, which is one */
    function int getBitSetter(int pos){
        var int bs, i;
        let bs = 1;
        while (i < pos){
            let bs = bs + 1;
        }
        return bs;
    }




}
